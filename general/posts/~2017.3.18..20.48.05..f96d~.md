---
type: post
date: ~2017.3.18..20.48.05..f96d
title: Reduction of Nock 8
author: ~taglux-nidsep
navsort: bump
navuptwo: true
comments: reverse
---

I typed this up for pastebin while helping out in urbit-meta, and it seems a waste not to save it somewhere for Nock newbies. 

definitions:
 
    *[a [b c] d] => [*[a b c] *[a d]]           :: reduction of nock tuple
    *[a 2 b c] => *[*[a b] *[a c]]              :: reduction of nock 2
    *[a 7 b c] => *[a 2 b 1 c]                  :: macro reduction of nock 7
    *[a 8 b c] => *[a 7 [[7 [0 1] b] 0 1] c]    :: macro reduction of nock 8
 
For reductions, you need the '*', which tells you what the nock interpreter will reduce.  `[0 1]` isn't the same as `*[0 1]`.
 
Reduction of nock 8:  `*[a 8 b c]`
 
    1.  *[a 7 [[7 [0 1] b] 0 1] c]                       :: def. of nock 8
    2.  *[a 2 [[7 [0 1] b] 0 1] 1 c]                     :: def. of nock 7
    3.  *[a 2 [[2 [0 1] 1 b] 0 1] 1 c]                   :: def. of nock 7
    4.  *[*[a [[2 [0 1] 1 b] 0 1]] *[a 1 c]]             :: def. of nock 2
    5.  *[[*[a [2 [0 1] 1 b]] *[a 0 1]] *[a 1 c]]        :: def. of nock tuple
    6.  *[[*[*[a [0 1]] *[a 1 b]] *[a 0 1]] *[a 1 c]]    :: def. of nock 2
 
How ugly!  Let's simplify, using the definitions of nock 0 and nock 1.  
 
    7.  *[[*[*[a [0 1]] *[a 1 b]] *[a 0 1]] c]      :: *[a 1 c] => c (def. of nock 1)
    8.  *[[*[*[a [0 1]] b] *[a 0 1]] c]             :: *[a 1 b] => b (def. of nock 1)
    9.  *[[*[*[a [0 1]] b] a] c]                    :: *[a 0 1] => a (def. of nock 0)
    10. *[[*[a b] a] c]                             :: *[a [0 1]] => a (def. of nock 0)
 
So, `*[a 8 b c]` reduces to `*[[*[a b] a] c]`.
## `~tonlur-sarret`
Ha, turns out this is because we don't have a `noun.hoon` mark in Arvo's `/mar` directory (there's a hack built into `%ford` to handle noun types until now and is useful for bootstrapping, but apparently this handles nouns in some places and not others). I just submitted a PR to add this in:
<https://github.com/urbit/arvo/pull/400>

The reason you weren't getting a stack trace for this was that you lost the error stack trace in your `%nounpasser` app's `++coup`. `++coup` is the arm where all acks are sent, whether the move was successful or unsuccessful. If your poke had succeeded and if you had gotten a successful ack, your apps would have worked of course. But because it failed, and because your `++coup` arm was constructed with `|=  *`, you lost that valuable stack trace and still produced your `[~ <+.$]`.

The right way to debug this (which should be more clear in our docs and examples) is to change `++coup` to the following:

```
::  nounpasser.hoon

...

++  coup
  |=  {wir/wire ack/(unit tang)}
  ?^  ack  (mean u.ack)
  ~&  %noun-acknowledged
  [~ +>.$]
--
```

which will crash if you try the poke again and produce something like this stack trace in `:dojo` (I'm running a fake `~zod` and `~nec` for this):

```
> :nounpasser 42
>=
[ ! /~zod/home/0/app/nounpasser:<[18 3].[20 11]>
  ! /~zod/home/0/app/nounpasser:<[18 12].[18 24]>
  ! fame: no /~nec/home/16/mar/noun
  ! ford: check [%noun [p=~nec q=%home r=[%ud p=16]] ~sapryl-fotbud]
  ! ford: vale %noun ~sapryl-fotbud
  ! mack
  [%ap-lame %nounpasser %coup]
]
```

Basically, the noun.hoon mark doesn't exist.

So, if you then create that file and copy in the code from my Github PR:

```
::
::::  /hoon/noun/mar
  ::
/?    310
!:
::::  A minimal noun mark
|_  non/*
++  grab  |%
          ++  noun  *
          --
--
```

and try again:

```
["I'm going to send this noun to a friend:" 42]
> :nounpasser 42
>=
%noun-acknowledged
~zod:dojo> 

...

["Here's the noun!:" 42]
~nec:dojo> 
```

Voil√†!
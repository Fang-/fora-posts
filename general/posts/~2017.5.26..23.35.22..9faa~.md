---
type: post
date: ~2017.5.26..23.35.22..9faa
title: Goldbach Conjecture in Hoon, Part 1
author: ~fittyn-fitsub-sigbex-hocdeb--misbud-hactyn-toddys-holryg
navsort: bump
navuptwo: true
comments: reverse
---

This is a Hoon sequel to my "Goldbach in Nock" posts.  As a reminder, the Goldbach conjecture is the following:

"Every even number greater than 2 is the sum of two primes."

If we can find one even number, greater than two, which isn't the sum of any two primes, then we'll have shown that the conjecture is false.  Any such number would be a counterexample to the conjecture.  The following is a Hoon program that will take an input and determine whether it's a counterexample, with comments.  Keep in mind that I don't know Hoon all that well yet; I'm on day 3 right now.  Take everything I say with a few grains of salt.

    !.                               :: Turn off stack trace
    |=  n/@                          :: Start gate, let n be the input
    =<  (goldbach-ce n)              :: Call "goldbach-ce" with n as sample
    |%                               :: Form a core
    ++  prime                        :: Prime checking function
      |=  b/@                        :: For this gate, see "prime" post
      =/  i  2                       :: 
      =/  j  2                       :: 
      ?:  (lth b 2)  %.n             :: 
      ?:  (lth b 4)  %.y             :: 
      |-                             :: 
      ?:  =((mul i j) b)  %.n        :: 
      ?:  =((add j 1) b)  %.y        :: 
      ?:  (gth (mul i j) b)          :: 
        $(i 2, j (add j 1))          :: 
      $(i (add i 1))                 :: 
    ::                               :: 
    ++  bothprime                    :: Checks if a pair is prime
      |=  {b/@ c/@}                  :: b and c are the samples
      ?:  &((prime b) (prime c))     :: If b and c are each prime then...
        %.y                          :: Return "yes"
      %.n                            :: otherwise "no"
    ::                               :: 
    ++  goldbach-ce                  :: The counterexample-testing gate
      |=  b/@                        :: b is the sample
      ?:  (lth b 4)                  :: If b < 4 then...
        %.n                          :: ...return "no"
      ?:  =((mod b 2) 1)             :: If b isn't even then...
        %.n                          :: ...return "no"
      =/  i  2                       :: let i = 2
      =/  j  (dec (dec b))           :: let j = b - 2. (Note: i + j = b)
      |-                             :: Start of loop
      ?:  (bothprime [i j])          :: If i and j are both prime then...
        [[i j] %.n]                  :: ...return i and j, and "no"
      ?:  =((add i 2) b)             :: If i + 2 = b then...
        %.y                          :: ...return "yes", otherwise...
      $(i (add 1 i), j (dec j))      :: Add 1 to i, take 1 from j, and loop
    --                               :: End of battery (for core)

To run this program in your dojo, do the following:

--If you haven't done it already, mount your home desk: enter `|mount %` in dojo.

--Paste the above program into a text editor and save it as "goldbachce.hoon" in your gen directory.

--Test it out by entering the following in dojo: `+goldbachce 112`

The result should be `[[3 109] %.n]`, because 3 + 109 = 112 (3 and 109 are prime), and `%.n` because, no, 112 isn't a counterexample to the Goldbach conjecture.

By the way, if you do find a Goldbach counterexample with this program, be sure to cite `~taglux-nidsep` in your groundbreaking mathematics paper!
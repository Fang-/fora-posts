---
type: post
date: ~2017.2.3..06.47.17..4ced
title: A new floating(-point) city
author: ~rabret-toptes-bonnev-lonmec--malzod-lisryc-doztux-marzod
navsort: bump
navuptwo: true
comments: reverse
---

Several time I've scanned through hoon.hoon looking at functions, and ended up slogging through the current floating-point code, leading me to think "why is there so much copy-pasting in this"

So like a week ago I played around with programmatically generating the cores, and got it working:

```
:-  %say
|=  {^ $~ $~}
=>  |%
++  land  ::  rd
  (gen-float @rd %*(. ff w 11, p 52, b --1.023))
::
++  lans  ::  rs
  (gen-float @rs %*(. ff w 8, p 23, b --127))
::
++  gen-float                                           ::  make fp core
  |*  {typ/_@ ma/_ff}
  |_  r/?($n $u $d $z)
  ::
  ++  ma
    %*(. ^ma r r)
  ::
  ++  sea                                               ::  typ to fn
    |=  {a/typ}  (sea:ma a)
  ::
  ++  bit                                               ::  fn to typ
    |=  {a/fn}  ^-  typ  (bit:ma a)
  ::
  ++  add  ~/  %add                                     ::  add
    |=  {a/typ b/typ}  ^-  typ  ~|  %fp-fail
    (add:ma a b)
  ::
  ++  sub  ~/  %sub                                     ::  subtract
    |=  {a/typ b/typ}  ^-  typ  ~|  %fp-fail
    (sub:ma a b)
  ::
  ++  mul  ~/  %mul                                     ::  multiply
    |=  {a/typ b/typ}  ^-  typ  ~|  %fp-fail
    (mul:ma a b)
  ::
  ++  div  ~/  %div                                     ::  divide
    |=  {a/typ b/typ}  ^-  typ  ~|  %fp-fail
    (div:ma a b)
  ::
  ++  fma  ~/  %fma                                     ::  fused multiply-add
    |=  {a/typ b/typ c/typ}  ^-  typ  ~|  %fp-fail
    (fma:ma a b c)
  ::
  ++  sqt  ~/  %sqt                                     ::  square root
    |=  {a/typ}  ^-  typ  ~|  %fp-fail
    (sqt:ma a)
  ::
  ++  lth  ~/  %lth                                     ::  less-than
    |=  {a/typ b/typ}  ~|  %fp-fail  (lth:ma a b)
  ++  lte  ~/  %lte                                     ::  less-equals
    |=  {a/typ b/typ}  ~|  %fp-fail  (lte:ma a b)
  ++  equ  ~/  %equ                                     ::  equals
    |=  {a/typ b/typ}  ~|  %fp-fail  (equ:ma a b)
  ++  gte  ~/  %gte                                     ::  greater-equals
    |=  {a/typ b/typ}  ~|  %fp-fail  (gte:ma a b)
  ++  gth  ~/  %gth                                     ::  greater-than
    |=  {a/typ b/typ}  ~|  %fp-fail  (gth:ma a b)
  ::
  ++  sun  |=  {a/@u}  ^-  typ  (sun:ma a)              ::  uns integer to typ
  ++  san  |=  {a/@s}  ^-  typ  (san:ma a)              ::  sgn integer to typ
  ++  sig  |=  {a/typ}  ^-  ?  (sig:ma a)               ::  get sign
  ++  exp  |=  {a/typ}  ^-  @s  (exp:ma a)              ::  get exponent
  ++  toi  |=  {a/typ}  ^-  (unit @s)  (toi:ma a)       ::  round to integer
  ++  drg  |=  {a/typ}  ^-  dn  (drg:ma a)              ::  typ to decimal float
  ++  grd  |=  {a/dn}  ^-  typ  (grd:ma a)              ::  decimal float to typ
  --
--
::
:-  %noun
:~
  (sqt:lans .2)
  (sqt:land .~2)
  (sqt:~(. lans %u) .2)
==
```

The problem comes when you want to leave the ground: you can't properly attach a jet engine to the core due to identical mugs or something (apparently even though I was under the assumption wet gates were monomorphized, this is actually a horrible lie). Hell, even running the above code will spew jet errors to your terminal thanks to the `%sqt` hint.

So, great and mighty `/fora`, anyone have a solution to the problem? It would clean up the stdlib a not-insignificant amount.

(I'm `~rys` but the planet I've been using on laptop has been dead for a while, so no SSO for me)
---
type: post
date: ~2017.2.14..09.04.35..6983
title: addition in nock
author: ~taglux-nidsep
navsort: bump
navuptwo: true
comments: reverse
---

You want a nock function that takes an ordered pair (e.g. [23 45]) and returns the sum (68).

Here's how the algorithm works.  We have N1 and N2 as the inputs, and we'll use the variable k as a counter (in noob-speak):

let k = 0

:start
If k = N2 then return N1
else add to 1 to k, add 1 to N1, and go to :start

Pretty straight-forward-- we add 1 to N1 k times, where k = N2.

Initially, the subject for our nock program is the ordered pair to be summed.  Let's "initialize" a variable by putting a zero in front of that:

[8 [1 0] a]

Now the subject should be [0 [N1 N2]] for a.

We need to attach a battery, which will consist of a single formula (our loop).  Then we'll have a core, and we can then activate the formula in the battery:

a:  [8 [1 b] [9 2 [0 1]]]

When b is activated the subject will be [b [0 [N1 N2]]].  (Notice that b is in tree address 2.)  Let's replace "a" in the initial formula above to get:

[8 [1 0] [8 [1 b] [9 2 [0 1]]]]

For our loop, we want a conditional which will be true when N2 equals the counter, and otherwise false:

b:  [6 [5 [0 15] [0 6]] [0 14] [9 2 c]]

Note that the tree address for the counter is 6, for N1 it's 14, and for N2 it's 15.  Because we want a loop, we're activating the same arm (tree address 2) until the conditional is true.  We need to pass a modified core, c, so that 1 is added to the counter and to N1 every cycle.  We'll keep the battery and N2 as is.

c:  [[0 2] [[4 [0 6]] [[4 [0 14]] [0 15]]]]

The whole thing, substituting in for b and c:

[8 [1 0] [8 [1 [6 [5 [0 15] [0 6]] [0 14] [9 2 [[0 2] [[4 [0 6]] [[4 [0 14]] [0 15]]]]]]] [9 2 [0 1]]]]

To run this in dojo:

.*([23 45] [8 [1 0] [8 [1 [6 [5 [0 15] [0 6]] [0 14] [9 2 [[0 2] [[4 [0 6]] [[4 [0 14]] [0 15]]]]]]] [9 2 [0 1]]]])

You'll get "68"!  Hurrah!

(Disclaimer: I'm not a tech guy so this might not be a good guide.  I wrote what I think might have been helpful to me when I was trying to figure it out.)

---
type: post
date: ~2017.3.4..06.04.28..6c91
title: 'greater than' in nock
author: ~taglux-nidsep
navsort: bump
navuptwo: true
comments: reverse
---

Let's say you have two unsigned integers, N1 and N2, and you want to know whether N1 is larger.  This is the nock program for you!  We'll pass [N1 N2], and it'll return '0' for yes, and '1' for no, just like all good Martian truth functions.

For the algorithm, let's use two variables as counters, i and j.  In pseudo-code:

1. i = N1
2. j = N2
3. if N1 = N2 then return 1 and terminate
4. if i+1 = N2 then return 1 and terminate
5. if j+1 = N1 then return 0 and terminate
6. add 1 to i, add 1 to j, and Goto 4

The idea is that by increasing i and j by 1 over and over, eventually one or the other will equal its opposite (N2 and N1, respectively).  And then we'll know which of N1 and N2 is larger.

I want to use the 'greater than' function in a later nock program, so I'll make sure to write it accordingly.  Notice, however, that in the algorithm above there are two parts---one part is run once, and one loops.  In order to write a nock program that behaves like this, we'll need to create a non-gate core and call that, instead of calling a gate.  (Maybe there's a way to do it purely in terms of gates, but it seemed easier not to do it that way.)

To begin:

[8 [[1 [[CB1 CB2] [[[1 0] [1 0]] [0 1]]]] [1 0]] w]

There's a lot here.  First, CB1 and CB2 are formulas that we will turn into arms before calling them.  CB1 is for the arm that we'll run exactly once, and CB2 is for the loop arm.  The tuple [[1 0] [1 0]] is for our two variables, i and j.

The initial subject is [N1 N2], so here is the subject that is passed to w:

[[[CB1 CB2] [[[1 0] [1 0]] [0 1]]] 0] [N1 N2]

We want w to do two things: (i) create a battery with the two arms we want, and (ii) call the first arm that battery with the right core as the subject.

w:

[8 [9 2 [0 2]] a]

To evaluate [9 2 [0 2]], we take [0 2] of the subject, [[[CB1 CB2] [[[1 0] [1 0]] [0 1]]] 0], and apply it to [0 2] of the latter, [[CB1 CB2] [[[1 0] [1 0]] [0 1]]].  The result is (fudging a bit for the unknowns, CB1 and CB2):

[[Arm 1] [Arm 2]] [[0 0] z], where z = [[[CB1 CB2] [[[1 0] [1 0]] [0 1]]] 0].

So the subject that is passed to a is:

[[[Arm 1] [Arm 2]] [[0 0] z]] [z [N1 N2]]

We have our battery in the subject.  'a' must call the first arm in that battery.  Before making the call, however, we need to make sure that we have a standardized core format that we pass as the subject.  (Every time we call up any function, we need to pass it a subject formatted in a way it expects.  Otherwise bad stuff can happen.)

The following standard should look familiar from previous posts:

[[battery] [[argument(s)] [context]]]

So, for a:

[9 4 [[0 4] [0 7] [0 5]]]

[battery] = [0 4] = [[Arm 1] [Arm 2]]
[argument(s)] = [0 7] = [N1 N2]
[context] = [0 5] = [[0 0] z]

Thus, we're passing the subject: [[Arm 1] [Arm 2]] [[N1 N2] [[0 0] z]]

...to [Arm 1].

All that's left is to figure out what's in CB1 and CB2 so that we know what Arm 1 and Arm 2 look like.  So, a lot of work left.

CB1, 'if N1 = N2 then return 1, else b':

[1 [6 [5 [0 14] [0 15]] [1 1] b]]

Double check to see that address # 14 is N1 and address #15 is N2.

b, 'call the loop, letting i be N1 and j be N2':

[9 5 [[0 2] [[0 6] [[0 6] [0 15]]]]]

This call keeps our battery the same ('[0 2]'), which is good, and replaces the [0 0] which stood for i and j, respectively, with the values for N1 and N2.  Otherwise, it keeps the context, z, the same ('[0 15]').  Our subject when we call the loop arm is:

[[Arm 1] [Arm 2]] [[N1 N2] [[N1 N2] z]]

(Again, the second [N1 N2] is because i = N1 and j = N2.)

Let's look at our loop formula.

CB2, 'if i+1 = N2 then return 1, else c':

[1 [6 [5 [4 [0 28]] [0 13]] [1 1] c]]

c, 'if j+1 = N1 then return 0, else d':

[6 [5 [4 [0 29]] [0 12]] [1 0] d]

The formulas above are fairly straightforward, though it may be instructive to check the addresses referenced against the subject.

d, 'call the loop arm again, adding 1 to i and adding 1 to j':

[9 5 [[0 2] [[0 6] [[[4 [0 28]] [4 [0 29]]] [0 15]]]]]

By now this should be pretty much as expected.  We're passing the same subject, except that we're incrementing address #'s 28 and 29 by 1.  We're passing it to address # 5, which is the loop arm.

And that's it!  We have our program. 

CB1, as a whole:

[1 [6 [5 [0 14] [0 15]] [1 1] [9 5 [[0 2] [[0 6] [[0 6] [0 15]]]]]]]

CB2, as a whole:

[1 [6 [5 [4 [0 28]] [0 13]] [1 1] [6 [5 [4 [0 29]] [0 12]] [1 0] [9 5 [[0 2] [[0 6] [[[4 [0 28]] [4 [0 29]]] [0 15]]]]]]]]

Whole program, all substitutions made:

[8 [[1 [[[1 [6 [5 [0 14] [0 15]] [1 1] [9 5 [[0 2] [[0 6] [[0 6] [0 15]]]]]]] [1 [6 [5 [4 [0 28]] [0 13]] [1 1] [6 [5 [4 [0 29]] [0 12]] [1 0] [9 5 [[0 2] [[0 6] [[[4 [0 28]] [4 [0 29]]] [0 15]]]]]]]]] [[[1 0] [1 0]] [0 1]]]] [1 0]] [8 [9 2 [0 2]] [9 4 [[0 4] [0 7] [0 5]]]]]

And here it is, a little easier to read:

    [ 8
      [ [ 1
          [ [1 6 [5 [0 14] [0 15]] [1 1] 9 5 [0 2] [0 6] [0 6] [0 15]]
            1
            6
                [5 [4 [0 28]] [0 13]]
                [1 1]
                [6
                    [5 [4 [0 29]] [0 12]]
                    [1 0]
                    [9 5 [0 2] [0 6] [[[4 [0 28]] [4 [0 29]]] [0 15]]]
                ]
          ]
          [[1 0] [1 0]]
          [0 1]
        ]
        [1 0]
      ]
       [8
          [9 2 [0 2]]
          [9 4 [[0 4] [0 7] [0 5]]]
       ]
    ]

To test whether 23 is greater than 45 in dojo, enter:

.*([23 45] [8 [[1 [[[1 [6 [5 [0 14] [0 15]] [1 1] [9 5 [[0 2] [[0 6] [[0 6] [0 15]]]]]]] [1 [6 [5 [4 [0 28]] [0 13]] [1 1] [6 [5 [4 [0 29]] [0 12]] [1 0] [9 5 [[0 2] [[0 6] [[[4 [0 28]] [4 [0 29]]] [0 15]]]]]]]]] [[[1 0] [1 0]] [0 1]]]] [1 0]] [8 [9 2 [0 2]] [9 4 [[0 4] [0 7] [0 5]]]]])

It should return 1 for 'no'.
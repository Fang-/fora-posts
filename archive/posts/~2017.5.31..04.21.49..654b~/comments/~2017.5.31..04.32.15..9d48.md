## `~sivtul-polpes-minmex-talryl--darmes-hasmed-dozdux-binzod`
Darn, I noticed a mistake right after hitting send...

I accidentally pasted the same program twice!  The second program should be:

```
::
:: goldbach-node.hoon
::
::
:: This program, when poked with a noun (which happens to be an ordered pair of
:: integers), will test each number in that range to see if it's a counter-
:: example to the Goldbach conjecture.  If so, send a positive report to the
:: mothership! (The "mothership" is an Urbit ship, and presumably the source
:: of the initial poke.)  Otherwise, send a negative report.
::
::
/?    314
!.                              
|%
  ++  move  {bone term wire *}  :: Define move.
--                              ::
|_  {bowl state/$~}             :: This program is stateless. 
::                              ::
++  prime                       :: This core is our prime-checker.
|=  n/@                         :: Takes n as the number to be tested.
  ^-  @f                        :: We cast for a loobean, because that's the
                                :: desired output for this core. "%.y" for
                                :: prime, "%.n" for not prime.
  ?:  (lth n 2)                 :: If n < 2, it's not prime.
    %.n                         :: Just say no! (Return "%.n")
  ?:  (lth n 4)                 :: If n < 4, it is prime!
    %.y                         :: 2 and 3 are prime! (Return "%.y")
  =/  count  2                  :: A regular counter.
  =/  lower  1                  :: Lower bound variable.
  =/  upper  n                  :: Upper bound variable.
  =/  test-value  (div (add n 1) 2)     :: Set the test-value to the average of
                                        :: the upper and lower bounds.
  |-                                    :: Start a loop core.
  ?:  =((mul test-value count) n)       :: If test * count = n then...
    %.n                                 :: ...n isn't prime.
  ?:  =(count +(p:(sqt n)))             :: If count = sqrt(n) + 1 then...
    %.y                                 :: ...n is prime.
  ?:  =(+(lower) upper)                 :: If lower + 1 = upper then...
    ?:  =((mul +(test-value) count) n)  :: If (test+1) * count = n then...
      %.n                               :: n isn't prime.  Otherwise...
    $(count +(count), lower 2, upper (dec n), test-value (div (add n 1) 2))
                                        :: Do all the following:
                                        :: count++, lower = 2, upper = n - 1,
                                        :: test-value = average(n + 1, 2),
                                        :: and loop.
  ?:  (gth (mul test-value count) n)    :: If test * count > n, then
    $(upper test-value, test-value (div (add test-value lower) 2))
                                      :: Do all the following:
                                      :: upper = test-value, 
                                      :: test-value = avrg(test-value, lower)
                                      :: and loop.
  $(lower test-value, test-value (div (add test-value upper) 2))
                                      :: Otherwise, do this:
                                      :: lower = test-value,
                                      :: test-value = avrg(test-value, upper)
                                      :: and loop.
::                                    ::
++  bothprime                         :: This core takes a pair of atoms, and
  |=  {b/@ c/@}                       :: says whether both are prime.
  ^-  @f                              :: Cast for loobean.
  ?:  &((prime b) (prime c))          :: If b and c are prime, then...
    %.y                               :: return "yes".
  %.n                                 :: Otherwise "no".
::                                    ::
++  goldbach-ce                       :: This core tests an atom to determine
  |=  b/@                             :: whether it's a counterexample.
  ^-  @f                              :: Cast for loobean.
  ?:  (lth b 4)                       :: If b < 4 then...
    %.n                               :: ...no, it isn't.
  ?:  =((mod b 2) 1)                  :: If b is odd then...
    %.n                               :: ...no, it isn't.
  =/  i  2                            :: Let i = 2.
  =/  j  (dec (dec b))                :: Let j = n - 2.
                                      :: (Note that i + j = n.)
  |-                                  :: Start loop core.
  ?:  (bothprime [i j])               :: If i and j are prime, then
    %.n                               :: no, n isn't a CE.
  ?:  =((add i 2) b)                  :: If i + 2 = b, then
    %.y                               :: Yes, n is a CE.
  $(i +(i), j (dec j))                :: Otherwise, do the following:
                                      :: Add 1 to i, take 1 from j...
                                      :: (noticing that i + j still equals n)
                                      :: and loop.
::                                    ::
++  poke-noun                         :: This core is for when this program
  |=  a/*                             :: gets its assignment from mothership!
  =/  b  +2:a                         :: Sample is a noun, let's roughly split
  =/  c  +3:a                         :: it into two parts. (Starting number & 
  =/  g  (@ b)                        :: ending number.) Then let's convert
  =/  h  (@ c)                        :: them to atoms.
  ^-  {(list move) _+>.$}             :: Cast list of moves, state unchanged.
  =/  mothership                      :: This sets the mothership.
    ~fittyn-fitsub-sigbex-hocdeb--misbud-hactyn-toddys-holryg
  ~&  ["Task received:" [g h]]        :: Notify user task is assigned.
  =/  whichway  |-  ^-  @f            :: Assign a loobean to "whichway"...
                                      :: using a loop core to determine the
                                      :: loobean assigned.
  ?:  (goldbach-ce g)                 :: If g is a CE...
    ~&  ["Golden Ticket!:" g]         :: ...then notify user and...
    %.y                               :: return "yes". 
  ?:  =(g h)                          :: If g = h, then...
    ~&  ["Check complete. Sending negative report."]
                                      :: test is complete, notify user and...
    %.n                               :: ...return "no".
  $(g +(g))                           :: Else, add 1 to g and loop.
  ?:  whichway                        :: If whichway = %.y, then
    [[[ost %poke /sending [mothership %goldbach-cluster] %atom g] ~] +>.$]
                                      :: Send a positive report to mothership.
  [[[ost %poke /sending [mothership %goldbach-cluster] %urbit our] ~] +>.$]
::                                    :: Otherwise send a negative report.
++  coup
  |=  {wir/wire ack/(unit tang)}
  ?^  ack  (mean u.ack)
  ~&  %report-received                :: Notify user when mothership gets word.
  [~ +>.$]
--
```

My apologies!  If some enterprising Tlon employee wants to replace the duplicated program with this in the OP, I'd greatly appreciate it.
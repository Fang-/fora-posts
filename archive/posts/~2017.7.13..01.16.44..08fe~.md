---
type: post
date: ~2017.7.13..01.16.44..08fe
title: Functor and Applicative
author: ~fodwyt-nimbus
navsort: bump
navuptwo: true
comments: reverse
---

I've been thinking about how to represent things like Functor and Applicative
in hoon.  The below code is submitted for your consideration. It sketches out
a convention for the classes (`++applicative` and `++functor`), instances
(`++unit-fmap` etc), and as a proof of concept one function that is generic
over the notion of an applicative thing (`++applicative-fmap`). The calls at
the bottom are for demonstration, and indeed demonstrate that the derived fmap
for unit is equivalent to the hand-written one.

Readers may be reminded of structures/signatures from ML. Or maybe not.
Comments are welcome.

    =+  |%
        ++  applicative
          |_  f/$-(mold mold)
          +-  apply  |*  {a/mold b/mold}
                     $-({(f $-(a b)) (f a)} (f b))
          +-  pure   |*  a/mold
                     $-(a (f a))
          --
        ++  functor
          |_  f/$-(mold mold)
          +-  fmap  |*  {a/mold b/mold}
                    $-  {$-(a b) (f a)}
                    (f b)
          --
        --
    =+  |%
        ++  applicative-fmap
          |*  {f/$-(mold mold) a/mold b/mold}
          |=  $:  apply/(~(apply applicative f) a b)
                  pure/(~(pure applicative f) $-(a b))
              ==
          ^-  (~(fmap functor f) a b)
          |=  {g/$-(a b) x/(f a)}
          ^-  (f b)
          (apply (pure g) x)
        --
    =+  |%
        ++  unit-pure
          |*  a/mold
          ^-  (~(pure applicative unit) a)
          |=  x/a
          ^-  (unit a)
          [~ x]
        ++  unit-apply
          |*  {a/mold b/mold}
          ^-  (~(apply applicative unit) a b)
          |=  {f/(unit $-(a b)) x/(unit a)}
          ^-  (unit b)
          ?~  f  ~
          ?~  x  ~
          [~ (u.f u.x)]
        ++  unit-fmap
          |*  {a/mold b/mold}
          ^-  (~(fmap functor unit) a b)
          |=  {f/$-(a b) x/(unit a)}
          ^-  (unit b)
          ?~  x  ~
          `(f u.x)
        --
    :*  ((unit-apply @ @) `dec `43)
        ((unit-pure @) 42)
        ((unit-fmap @ @) dec `43)
        %+  ((applicative-fmap unit @ @) (unit-apply @ @) (unit-pure $-(@ @)))
          dec
        `43
    ==

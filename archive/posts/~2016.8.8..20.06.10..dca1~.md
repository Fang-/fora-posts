---
type: post
date: ~2016.8.8..20.06.10..dca1
title: "List comprehensions" in Hoon
author: ~ladhut-docfyn
navsort: bump
navuptwo: true
comments: reverse
---

Last winter I was out of power for about a week thanks to a freaky windstorm. I learned a few things:
- Being stuck in a dark, forty degree house is super boring.
- The novelty of reading by candlelight wears out long before the candle has lost any appreciable height.
- Hoon's dense pronounceable syntax is pretty much ideal for coding in your head.

So I spent some time pacing around in a big dark walk-in refrigerator thinking about how to do list comprehensions in Hoon. Once the power came back on I implemented my approach, then subsequently forgot all about it. (Hey, it was a dark time for me.)

Here's a 151-ized version that uses the same comprehension to compute both the list and the set of all Pythagorean triples involving numbers <= 20.

```
!:
=<  =/  triples
      %+  for  (gulf 1 20)  |=  a/@
      %+  for  (gulf 1 20)  |=  b/@
      %+  for  (gulf 1 20)  |=  c/@
      =/  ab2  (add (mul a a) (mul b b))
      %+  if   =(ab2 (mul c c))  |.
      %-  use  [a b c]
    ::
    :*  %-  (list-comp {@ @ @})  triples
        %-  (set-comp {@ @ @})   triples
    ==
=~
|%
++  step  $-({* *} *)
++  comp  _|*({rv/* st/step} (st rv **))
--
|%
::
++  list-comp  |*(a/mold (onto *(list a)))
::
++  set-comp   |*(a/mold (into *(set a)))
::
++  use
  |*  val/*
  |*  {rv/* st/step}
  (st rv val)
::
++  if
  |*  {cond/? k/_|.(comp)}
  |*  {rv/* st/step}
  ?.  cond  rv
  ((k) rv st)
::
++  for
  |*  {xs/(list) k/_|*(* comp)}
  |*  {rv/* st/step}
  |-  ?~  xs  rv
  $(rv ((k i.xs) rv st), xs t.xs)
::
++  onto                                                ::  cons onto list
  |*  init/(list)
  |*  k/comp
  %-  flop
  ^+  init
  %+  k  init
  |*  {rv/_init x/*}  ^+  init
  [x rv]
::
++  into                                                ::  put into set
  |*  init/(set)
  |*  k/comp
  %+  k  init
  |*  {rv/_init x/*}  ^+  init
  (~(put in rv) x)
--
==
```

I'm guessing this is approximately never practical in real code, but it's neat that it works. It also shows that Hoon is tantalizingly close to enabling a reasonable facsimile of Haskell's "do notation", which is just a generalized kind of list comprehension where the result appears at the end instead of the beginning. Aside from the column of runes, the biggest difference is the order in which bindings appear. All it would take to close the gap is a macro (call it `#+`) that expanded from

```
#+  c  a  b
d
```

to

```
%+  a  b  |=  c
d
```

I'm not actually suggesting such a macro be implemented, but thought it was worth noting the possibility.
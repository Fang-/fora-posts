---
type: post
date: ~2016.8.2..21.19.29..2ab8
title: Styled output - requirements and progress
author: ~ropdeb-sormyr
navsort: bump
navuptwo: true
comments: reverse
---

# Styled output - requirements and progress

Recently, I've been working on a mechanism for printing styled text in Urbit.
Some early use-cases include prompt disambiguation and syntax errors in
`:dojo`, and audience/channel clarity in `:talk`. I've done some initial type
design and prototyping (at
[urbit/arvo#212](https://github.com/urbit/arvo/issues/212) and
[urbit/arvo#216](https://github.com/urbit/arvo/pull/216/files?w=1), now moving
the discussion here.

First, a rundown of the current prototype:

### user-facing types

The design is informed by the capabilities of the terminal -- a true 
typographical least-common-denominator:

> Text decorations:
> 
> bright, underlined, or blinking
>
> Foreground (text) and background colors:
> 
> red, green, blue, cyan, magenta, yellow, black, or white

Our first molds are obvious:

```
++  deco  ?($~ $bl $br $un)                             ::  text decoration
++  tint  ?($~ $r $g $b $c $m $y $k $w)                 ::  text color
```

Styled text can combine decorations with separate foreground and background
colors, so a style definition should include all three:

```
++  styl                                                ::  deco/bg/fg
  (pair (unit deco) (pair (unit tint) (unit tint)))     ::  cascading style
```

We use units to allow for style inheritance: `~` means inherit, while `[~ ~]`
means *none*.

Finally, we need to combine our style definition with some text:

```
++  styx  (list $@(@t (pair styl styx)))                ::  styled text
```

That mold is a nifty piece of Hoonery (courtesy of Curtis):

- a superset of `tape` -- unstyled text is as simple as possible
- supports the obvious use-case: "a list of style/text pairs"
- but it's recursive, so you can nest styled pairs, and cascade/inherit styles

`++styx` examples:

```
"foo"
[[[~ ~ `%r] "foo"] [[~ ~ `%g] "bar"] ~]
(welp "entering " [[~ `%k `%g] 'the ' [[`%un ~ ~] "matrix"] ~])
```

### output types

While an urbiter can "print" to the "console" using `~&` and it's ilk, that's
not very proper -- printf hints are designed as a debugging mechanism. The
blessed path is through `++sole-effect`, used by `:talk`, `:dojo` (and,
therefore, generators), and all manner of right-thinking applications. So, we
add to `++sole-effect` (defined in `sur/sole/hoon`):

```
{$klr p/styx}                                           ::  styled text line
```

And verily, all manner of applications could then, once-linked to `drum` via
`|link %app`, print styled output thusly:

```
=/  out/styx
  [[[~ ~ `%r] "foo"] [[~ ~ `%g] "bar"] ~]
[[ost %diff %sole-effect %klr out]~ +>.$]
```

### implementation types

`++sole-effect` is handled by in `lib/drum/hoon`, the library used by `:hood`
to manage (and rotate) the "drum" of console applications. A cursory `grep`
informs us that `++sole-effect` is received by `++ta-fec`, and then the magic
begins. The output effects (`%txt` and `%tan`) are sent through a number of
gates, (`++ta-tan`, `++se-text`, `++se-blit`), and converted, line by line, to
`(list @c)` which is handed off to the `dill` kernel module as a `++dill-blit`
move. In the interest of minimal disruption, we extend `++dill-blit`:

```
{$klr p/stub}                                           ::  styled text
```

But what is `++stub`?

Recall that normal console output is printed as `(list @c)`, a list of unicode
codepoints. (this is important for i18n, or, as it's known today, emoji.)
`tape` is `(list @tD)`: a list of "utf-8 flavored" bytes. And our mold `++styx`
uses `@t`, which is a text atom of any length. *We must convert.*

```
++  styd  (pair (set deco) (pair tint tint))            ::  decos/bg/fg
++  stub  (list (pair styd (list @c)))                  ::  styled tuba
```

To simplify the printing process, we flatten our `++styx` tree into a list of
pairs, reifying inherited styles along the way. We take a set of `++deco`,
since text decorations compose.

We also want styled prompts; we modify those in-place:

`++sole-prompt`:

```
$:  vis/?                                             ::  command visible
    tag/term                                          ::  history mode
    cad/styx                                          ::  caption
== 
```

`++dill-blit`:

```
{$pro p/stub}
```

The correct final step would be to interpret our style definitions and convert
them to ANSI escape codes in the native terminal implementation (`term.c`).
Alas, we have not yet done so. In the interest of rapid prototyping, we have
befouled Arvo with an artifact of Unix -- that conversion happens in `dill`.
(As it turns out, we can't currently use color codes -- I think something's
amiss with the `termios` configuration in `term.c`.)

Thus concludes our whirlwind tour of
[urbit/arvo#216](https://github.com/urbit/arvo/pull/216/files?w=1).

### `tank`

Jumping back to `++sole-effect` processing in `drum`, we can see that `%txt`
effects are converted to `[%tan [%leaf p.fec]~]` -- both output effects are
eventually processed as `%tan` by `++ta-tan`. `%tan` is a list of `tank`,
but what is a `tank`?

```
++  tank  $%  {$leaf p/tape}                            ::  printing formats
              $:  $palm                                 ::  backstep list
                  p/{p/tape q/tape r/tape s/tape}       ::
                  q/(list tank)                         ::
              ==                                        ::
              $:  $rose                                 ::  flat list
                  p/{p/tape q/tape r/tape}              ::  mid open close
                  q/(list tank)                         ::
              ==                                        ::
          ==                                            ::
```

`tank` is the diagnostic output format; stack traces and error messages are
printed as tanks. As we have just seen, all console output converges on tank.
And generators can use `tank` as well, via the `tang` mark (see `+ls`, `+cat`,
`+moon`, etc.).

We can print a list of `tank` with `++slog`:

```
> (slog ~[leaf+"foo"])
foo
<1.xbo {* <407.sxa 110.xht 1.ztu $151>}>
```

For convenience, `++slog` produces the identity gate, `++same`:

```
> same
<1.xbo {* <407.sxa 110.xht 1.ztu $151>}>
> (same ~)
~
> ((slog ~[leaf+"foo" leaf+"bar"]) ~)
foo
bar
~
```

But `tank` has some pretty complex-looking variants, and `slog`'ing them
doesn't provide much clarity:

```
> ((slog [%rose [["foo" "bar" "baz"] ~[leaf+":a:" leaf+":b:" leaf+":c:"]]]~) ~)
bar:a:foo:b:foo:c:baz
~
> ((slog [%palm [["foo" "bar" "baz" "bud"] ~[leaf+":a:" leaf+":b:" leaf+":c:"]]]~) ~)
barbaz:a:foo:b:foo:c:bud
~
```

In `drum`, `++ta-tan` handles `tank` thusly: `(~(win re a) [0 edg])`. 
*We must go deeper.*

### `++re`

`++re` is section 2eK of `arvo/hoon/hoon`: "formatting (layout)". It's a `|_`
(ie, `:door`, core with sample), accepting a `tank` and providing two arms:
`++ram`, producing a `tape`, and `++win`, producing a list of `tape`.

`++ram` behaves much like `++slog`, just one `tank` at a time:

```
> ~(ram re leaf+"foo")
"foo"
> ~(ram re [%rose [["foo" "bar" "baz"] ~[leaf+":a:" leaf+":b:" leaf+":c:"]]])
"bar:a:foo:b:foo:c:baz"
> ~(ram re [%palm [["foo" "bar" "baz" "bud"] ~[leaf+":a:" leaf+":b:" leaf+":c:"]]])
"barbaz:a:foo:b:foo:c:bud"
```

But `++win` is a gate, with a sample of two atoms: left-indent and column-length.
When we provide a column length less than our `tank`, all is revealed to us:

first, a helper (to print each tape on its own line):

```
> =print |=(a/wall ,:(head (turn a |=(a/tape ((slog [leaf+a]~) ~)))))
```

`%leaf` by itself is always one line:

```
> (print (~(win re leaf+"foo") [0 10]))
foo
~
```

`%rose` is one line, unless it's longer than our column length, in which case
it's indented to match `q`:

```
> =rose [%rose [[" - " "> " " <"] ~[leaf+"foo" leaf+"bar" leaf+"baz"]]]
> (print (~(win re rose) [0 80]))
> foo - bar - baz <
~
> (print (~(win re rose) [0 10]))
> 
  foo
  bar
  baz
 <
~
```

and `%palm` is one line, unless it's longer than our column length, in which
case it's backstep-indented:

```
> =palm [%palm [[" - " ">" ":" "<"] ~[leaf+"foo" leaf+"bar" leaf+"baz"]]]
> (print (~(win re palm) [0 80]))
>:foo - bar - baz<
~
> (print (~(win re palm) [0 10]))
>   foo
  bar
baz
~
```

A good example of this tank windowing is in `+cat`; if you `+cat` a directory,
you get a `%palm` with the path and directory as `%rose`s:

with a full-width terminal:

```
> +cat %/gen/cat
* /~bilrex-rontel-macbep-rovdun--paddes-wattuc-hinbyl-wollyn/home/~2016.8.2..18.54.33..ab30/gen/cat: hoon pretty/hoon
```

with a narrower terminal:

```
> +cat %/gen/cat
    *
    / ~bilrex-rontel-macbep-rovdun--paddes-wattuc-hinbyl-wollyn
      home
      ~2016.8.2..18.57.04..ba31
      gen
      cat
hoon pretty/hoon
```

Finally, there's one more layout feature provided by `++win` (it took me a bit to realize the [gating conditional was hardcoded](https://github.com/urbit/arvo/blob/26c674e9ae068cb3b2a1d9cb77f359c6d9055168/arvo/hoon.hoon#L3845)).
Uncommenting that conditional gives us `\/` windowing:

```
> =rose [%rose [[" - " "> " " <"] ~[leaf+(gulf 'a' 'z') leaf+(gulf 'a' 'z')]]]
> (print (~(win rez rose) [0 80]))
> abcdefghijklmnopqrstuvwxyz - abcdefghijklmnopqrstuvwxyz <
~
> (print (~(win rez rose) [0 10]))
> 
  \/abcd\/
    efgh
    ijkl
    mnop
    qrst
    uvwx
    yz
  \/    \/
  \/abcd\/
    efgh
    ijkl
    mnop
    qrst
    uvwx
    yz
  \/    \/
 <
~
```


### `tanq`

As we've seen, `tank` can be pretty complex. Maybe too complex, for a
diagnostic output format. There's a replacement waiting in the wings:

```
++  tanq                                                ::  future tank   
          $?  {$~ p/(list tanq)}                        ::  list of printables
              {$~ $~ p/tape}                            ::  simple string
              (pair @tas tanq)                          ::  captioned
          ==
```

Compared to `tank`, `tanq` is really minimal:

```
[~ (limo ~[[~ ~ "foo"] [~ ~ "bar"]])]
[~ ~ "foo"]
[%foo [~ ~ "bar"]]
```

Presumably, `{$~ $~ p/tape}` is a line and `{$~ p/(list tanq)}` is a list of
lines. I don't know if the caption is to be applied to every line it contains,
or simply precede them (once). And since there's not yet a `++re` equivalent
for `tanq`, it's a bit of an open question which of its features will be supported:

- left indentation
- line wrapping based on column length
- `\/` windowing

### next steps

I offer the preceding wall of text because there's a proposal to include
`++styx` (or something like it) in `tanq` (and maybe `tank`, to ease the
transition):

I see only one natural approach to integration: `{$~ $~ p/styx}`. As we've covered,
`++styx` is a superset of `tape`, so that's entirely compatible with
the current design.

As for a `++re` for `tanq`, left-indentation and line-wrapping seem like pretty
useful features to me. I'm not clear on when one would want `\/` windowing --
it's not a familiar idiom, and doesn't seem like a frequent requirement.

`termios` should be configured to support the full range of relevant ANSI
escape codes, and `++stub` (or something like it) should be interpreted in
`term.c`. I'm not sure how to do the former, but can probably handle the
latter.

Thoughts? Questions? Witty Remarks?
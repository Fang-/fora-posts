---
type: post
date: ~2017.7.2..21.47.49..a494
title: Random Ticket generation utility for Galaxies and stars.
author: ~fosnut-dandut
navsort: bump
navuptwo: true
comments: reverse
---

With the new influx of stars and galaxies I thought an all in one utility to issue tickets for random stars, planets, or moons might be helpful for testing purposes. So I dusted off my hoon skills and wrote one.

     ::  Generate a random ship and corresponding ticket respecting
    ::  the current ships rank.
    ::
    ::::  /hoon/ticketer/gen
      ::
    /?    310
    !:
    ::::
      ::
    :-  %say
    :: shiptype represents one of the three types of ship we can can randomly
    :: choose and issue a ticket for.
    =+  ^=  shiptype
          $%  {$planet $~}  :: We make planets the default
              {$star $~}
              {$moon $~}
          ==
    :: Ticketer is a core that encapsulates the logic of asking for a particular
    :: ship and ticket pair.
    =+  ^=  ticketer
          |_  {ask/shiptype now/@da eny/@uvJ bec/beak}
          ++  get-ticket  :: Gets a ticket for a given ship name
            |=  ship/@p
            .^(@ /a/(scot %p p.bec)/tick/(scot %da now)/(scot %p ship))
          ++  do-moon  :: generate a random moon and ticket pair
            =+  moon=(mix (lsh 5 1 (end 5 1 eny)) p.bec)
            =+  tic=(get-ticket moon)
            "moon: {<`@p`moon>}; ticket: {<`@p`tic>}"
          ++  do-planet  :: generate a random planet and ticket pair
            =+  planet=(mix (lsh 4 1 (end 4 1 eny)) p.bec)
            =+  tic=(get-ticket planet)
            "planet: {<`@p`planet>}; ticket: {<`@p`tic>}"
          ++  do-star  :: generate a random star and ticket pair
            =+  star=(mix (lsh 3 1 (end 3 1 eny)) p.bec)
            =+  tic=(get-ticket star)
            "star: {<`@p`star>}; ticket: {<`@p`tic>}"
          ++  handle-from-galaxy  :: Logic for when running from a a galaxy
            ?-  -.ask
            $star  do-star
            $planet  do-planet
            $moon  do-moon
            ==
          ++  handle-from-star  :: Logic for running from a star
            ?+  -.ask  "Stars can't create that type of ship"
            $star  do-star
            $planet  do-planet
            :: TODO(jwall): Can stars create moons?
            ::$moon  do-moon
            ==
          ++  handle-from-planet  :: Logic for running from a planet
            ?+  -.ask  "Planets can't create that type of ship"
            $moon  do-moon
            ==
          ++  get-ship-ticket
            :: Get the rank of this current ship
            ~&  -.ask
            ?-  (clan p.bec)
            $czar  handle-from-galaxy
            $king  handle-from-star
            $duke  handle-from-planet
            :: Moons and comets can't issue tickets
            $earl  "moons can't issue tickets"
            $pawn  "comets can't issue tickets"
            ==
          --
    |=  $:  {now/@da eny/@uvJ bec/beak}
            {ask/shiptype}
            $~
        ==
    :-  %tang  :_  ~  :-  %leaf
    ~(get-ship-ticket ticketer ask now eny bec)


Just paste this into gen/ticketer.hoon for your galaxy, star, or planet. and call from dojo with

    +ticketer %star

Where %star is one of %star, %planet, or %moon. This is most useful for stars since planet's already have a +moon command that will do the moon only logic.

A more ambitious soul than I might wish to turn it into a full fledged application that keeps track of claimed ship/ticket pairs.
---
type: post
date: ~2017.3.19..19.36.37..e7b5
title: A sketch of a unit testing framework
author: ~ponnys-nacwer
navsort: bump
navuptwo: true
comments: reverse
---

I will be up front about my biases: I come from an engineering culture where unit testing is civilization. It's sort of confusing to me that Urbit doesn't currently have a unit testing framework because it seems like having one would increase productivity: how can you have any confidence that your code still works across major refactors if there's no automated testing?

So what would a minimal testing framework look like? Here's an rough sketch. There should be:

- A testing library in `%/lib/tester/hoon`, which provides common primitives to tests.

- A set of tests suites `%/tests`. Each `.hoon` file is a core with a set of `++test-...` arms which get run.

- A generator which runs a specific test, a specific suite, or all available tests.

What would that look like? I've left massive TODOs in the sketch below, and I'll discuss afterwards.

```
|%
::  ----------------------------------------------------------------------
::  Eventually should be in %/lib/tester/hoon.
::  ----------------------------------------------------------------------
++  test-lib
  |%
  ++  init-test
    |=  {cookie/@uvJ}
    ~(. tester `(list tape)`~ cookie)
  ::
  ++  tester-type  _(init-test `@uvJ`0)
  ::
  ++  tester
    |_  {f/(list tape) c/@uvJ}
    ::  todo: unit testing libraries have a lot more to them than just eq.
    ++  expect-eq
      |=  {a/* b/* c/tape}
      ^+  +>
      ?:  =(a b)
        +>
      ::  todo: am i able to grab the stack trace here? that would be nice.
      ::  todo: render a and b to string form here for error messages.
      +>(f [c f])
    ::
    ++  results
      ::  returns results.
      ::
      ::  returns the test run's identity cookie and the list of failures.
      |.
      ^-  {@uvJ (list tape)}
      [c f]
    --
  --
::  ----------------------------------------------------------------------
::  Eventually should be in %/test/basic/hoon.
::  ----------------------------------------------------------------------
++  test-core
  |%
  ++  test-freedom
    |=  t/tester-type:test-lib
    =.  t  (expect-eq:t (add 2 2) 4 "freedom is the freedom to say...")
    t
  ++  test-a-failure
    |=  t/tester-type:test-lib
    =.  t  (expect-eq:t (add 2 2) 5 "freedom is the freedom to say...")
    t
  ++  test-crash
    |=  t/tester-type:test-lib
    !!
  --
::  ----------------------------------------------------------------------
::  Stays in the generator.
::  ----------------------------------------------------------------------
++  local
  |%
  ++  perform-test-suite
    ::  takes a testing core and executes all tests in it.
    |=  {name/tape v/vase eny/@uvJ}
    ^-  tang
    =+  core-arms=(sloe p.v)
    ::  todo: work around mint-vain
    =+  null-check=core-arms
    ?~  null-check
      [[%leaf :(weld "error: " name " is not a valid testing core.")] ~]
    =|  out/tang
    |-
    ?~  core-arms
      out
    %=  $
      out  (weld (perform-test-arm name i.core-arms v eny) out)
      core-arms  t.core-arms
    ==
  ::
  ++  perform-test-arm
    ::  performs a single test.
    |=  {suite-name/tape arm-name/term v/vase eny/@uvJ}
    ::  todo: terminal color on the output
    ^-  tang
    =+  run=(run-arm-in-test-core arm-name v eny)
    =+  full-name=:(weld suite-name "/" (trip arm-name))
    ?-  -.run
      $|  ::  the stack is already flopped for output?
          ;:  weld
            p:run
            `tang`[[%leaf (weld full-name " CRASHED")] ~]
          ==
      $&  ::  todo: test the cookie to make sure it returned the same core.
          ?:  =(~ +.p:run)
            [[%leaf (weld full-name " OK")] ~]
          ::  Create a welded list of all failures indented.
          %-  flop
          ;:  weld
            `tang`[[%leaf (weld full-name " FAILED")] ~]
            %+  turn  +.p:run
              |=  {i/tape}
              ^-  tank
              [%leaf (weld "  " i)]
          ==
    ==
  ::
  ++  run-arm-in-test-core
    ::  runs a single arm.
    ::
    ::  returns the output of `++mule` so that we can react to crashes
    ::  appropriately.
    |=  {arm-name/term v/vase eny/@uvJ}
    ^-  (each {@uvJ (list tape)} (list tank))
    =/  t  (init-test:test-lib eny)
    ::  run the tests in the interpreter so we catch crashes.
    %-  mule  |.
    =/  r  (slam (slap v [%limb arm-name]) !>(t))
    ::  return just the results or we will be here forever while we try to copy
    ::  the entire kernel.
    ((hard {@uvJ (list tape)}) q:(slym (slap r [%limb %results]) r))
  --
::  ----------------------------------------------------------------------
--
:-  %say
|=  $:  {now/@da eny/@uvJ bec/beak}
        $~
        $~
    ==
:-  %tang
::  todo: right now, we hard code ++test-core. but eventually, we must instead
::  scry ford for the core from the hoon file. that doesn't exist yet.
(perform-test-suite:local "test-core" !>(test-core) eny)
```

And when you run that generator, you get:

```
> +test
test-core/test-crash CRASHED
/~zod/home/0/gen/test:<[109 5].[112 69]>
/~zod/home/0/gen/test:<[109 12].[109 50]>
/~zod/home/0/gen/test:<[48 5].[48 7]>
test-core/test-a-failure FAILED
  freedom is the freedom to say...
test-core/test-freedom OK
~zod:dojo>
```

Right now, everything is hardcoded into a single generator file. I'd eventually want to have `+test /test-core/` and `+test /test-core/test-freedom/` to load the core from a `%/test/` directory, along with a command to run all tests on the current desk: `+test , =all`

Why run all tests on a desk? I'm trying to plan around the end state of app distribution on urbit. Let's say that I'm providing a library to the world. I have a desk for it: `/=mylib=/`. I have a few files in `/=mylib=/lib/`, `/=mylib=/sur/` and `/=mylib=/test/`. If dir is set to mylib in dojo, running `+test , =all` should run the system tests and the additional tests I wrote for my library.

A second person on a different ship has `/=theirapp=/`. Their app is quite handy, but syncs in `/=mylib=` since it relies on mylib's functionality. They also write tests in `/=theirapp=/test/`, and when they run `+test , =all`, it runs all the tests on the desk, which include the ones from mylib. If you depend on a component, you should be running the tests for that component.

To actually get loading of cores looks like it would take a single major change: adding a `%ford` scry namespace, so the generator could scry `%clay` for all the hoon files in `%/tests`, and then use those paths to scry `%ford` for an executable core that corresponded with the hoon file, with `%ford` resolving any `/+` and `/-` used in the test.

There's a lot of other things to think about. I left lots of TODOs in the sketch above, but there's also:

- Whether testing cores should be doors or not.

- Additional test types. Key the test expectation type off of a prefix `++test-` for normal tests, `++check-` for [QuickCheck][qc] style tests, `++crash-` for tests which assert that the test crashes, etc.

- Details of the testing library.

- Formatting of the output.

[qc]: https://wiki.haskell.org/Introduction_to_QuickCheck2
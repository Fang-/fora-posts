:-  :~
  [%owner '~pansyl-bantev-fipset-tocter--rolsev-dannev-binwyd-dopzod']
  [%type 'comments']
  [%last-modified '~2018.6.15..01.47.32..4152']
  [%date-created '~2018.6.15..01.47.32..4152']
    ==
;>

```
    =<  samefringe
    =~
    |%
    ++  stream  |*(a=mold (unit (pair a _|?(*(stream a)))))
    --
    |%
    ++  samefringe
      |=  [p=* q=*]
      ^-  ?
      =/  pl=(stream @)  (leaves p)
      =/  ql=(stream @)  (leaves q)
      |-  ^-  ?
      ?~  pl  ?~(ql & |)
      ?~  ql  |
      ?.  =(p.u.pl p.u.ql)  |
      $(pl (q.u.pl), ql (q.u.ql))
    ++  single
      |*  a=*
      ^-  (stream _a)
      `[a |?(*(stream _a))]
    ++  leaves
      |=  a=*
      ^-  (stream @)
      ?@  a  (single a)
      %+  (stream-cat @)
        $(a -.a)
      $(a +.a)
    ++  stream-cat
      |*  a=mold
      |=  (pair (stream a) (stream a))
      ^-  (stream a)
      ?~  p  q
      :+  ~  p.u.p
      |?  ^$(p (q.u.p))
    --
    ==
```



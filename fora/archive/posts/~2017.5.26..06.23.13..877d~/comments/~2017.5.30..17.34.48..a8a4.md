## `~fittyn-fitsub-sigbex-hocdeb--misbud-hactyn-toddys-holryg`
I didn't know Hoon had a native square root function, but after some digging in `Hoon.hoon`, I see that it does!  This will help us to use a much fast algo than either of the above.  But first we have to know how to use `sqt`.  It's not hard, but initially I assumed it worked like the other math functions, and got errors when using it as such in my program.  (It's always good to test stuff in dojo first.)

```
> (sqt 16)
[p=4 q=0]
> (sqt 15)
[p=3 q=6]
```

It's pretty clear what's happening here; the first number, `p`, is the largest integer that when squared isn't larger than the input value.  That leaves the remainder for `q`.  To extract the value I want for an improved prime checker, I need only prefix the command with `p:`...

```
> p:(sqt 16)
4
> p:(sqt 15)
3
```

With that, a significantly faster prime checker:

```
!.
|=  n/@                    
  ^-  ?               
  ?:  (lth n 2)
    %.n
  ?:  (lth n 4)
    %.y
  =/  count  2
  =/  lower  1
  =/  upper  n
  =/  test-value  (div (add n 1) 2)
  =/  step  1
  |-  
  ?:  =((mul test-value count) n)
    ~&  [%count count %low lower %up upper %test test-value %step step]
    %.n
  ?:  =(count +(p:(sqt n)))
    ~&  [%count count %low lower %up upper %test test-value %step step]
    %.y
  ?:  =(+(lower) upper)
    ?:  =((mul +(test-value) count) n)
      ~&  [%count count %low lower %up upper %test test-value %step step]
      %.n
    $(count +(count), lower 2, upper (dec n), test-value (div (add n 1) 2), step +(step))
  ?:  (gth (mul test-value count) n)
    $(upper test-value, test-value (div (add test-value lower) 2), step +(step))
  $(lower test-value, test-value (div (add test-value upper) 2), step +(step))
```

It still uses a naive algorithm, and so could be a lot faster, but this a decent mix between speed, transparency of the algo, and simplicity.  To compare algorithm speeds between this program and the first, you can use the following program, which measures how many loops each method takes for all numbers up to some input n.  Counting loops isn't a perfect apples to apples comparison, but it's a good first-order approximation.  Play around with your own favorite algorithm, if you like, and do better!

```
!.                              
|=  n/@                          
=<  (compare-up-to n)         
|%                              
++  prime                     
  |=  n/@                    
  =/  i  2                 
  =/  j  2
  =/  step  1
  ^-  @f
  ?:  (lth n 2)
    %.n
  ?:  (lth n 4)
    %.y
  |-  
:: ~&  [%i i %j j %step step]
  ?:  =((mul i j) n)
    ~&  [%slow %n n %step step %not-prime]
    %.n
  ?:  =((add j 1) n)
    ~&  [%slow %n n %step step %prime]
    %.y
  ?:  (gth (mul i j) n)
    $(i 2, j (add j 1), step +(step))
  $(i (add i 1), step +(step))
++  primes-match               
  |=  b/@                     
  ^-  @f
  =/  g  (prime b)
  =/  h  (fast-prime b)
  ?:  &(g h)                   
    %.y                       
  ?:  &(!g !h)
    %.y
  %.n                        
::                              
++  fast-prime
  |=  n/@                    
  ^-  ?               
  ?:  (lth n 2)
    %.n
  ?:  (lth n 4)
    %.y
  =/  count  2
  =/  lower  1
  =/  upper  n
  =/  test-value  (div (add n 1) 2)
  =/  step  1
  |-  
  ?:  =((mul test-value count) n)
    ~&  [%fast %n n %step step %not-prime]
    %.n
  ?:  =(count +(p:(sqt n)))
    ~&  [%fast %n n %step step %prime]
    %.y
  ?:  =(+(lower) upper)
    ?:  =((mul +(test-value) count) n)
      ~&  [%fast %n n %step step %not-prime]
      %.n
    $(count +(count), lower 2, upper (dec n), test-value (div (add n 1) 2), step +(step))
  ?:  (gth (mul test-value count) n)
    $(upper test-value, test-value (div (add test-value lower) 2), step +(step))
  $(lower test-value, test-value (div (add test-value upper) 2), step +(step))
::
++  compare-up-to
  |=  end/@
  ^-  @f
  =/  count  1
  |-
  ?:  =(count +(end))
    %.y
  ?:  (primes-match count)
    $(count +(count))
  ~&  count
  %.n
--         
```
---
type: post
date: ~2017.7.13..22.31.58..aa52
title: Use the Source
author: ~tonlur-sarret
navsort: bump
navuptwo: true
comments: reverse
---

Too often people ask us the best way to learn Hoon and Arvo. One answer: use the <strike>force</strike> source.

> This post expects that you have a copy of the [Arvo source repository](https://github.com/urbit/arvo) on your local machine, and that you're checked out to the maintenance `maint-20170614` branch via `git`. It's also probably a good idea to have worked through the [Urbytes](https://urbit.org/docs/byte/) and the [intro Hoon docs](https://urbit.org/docs/hoon/). But neither is required.  You don't need to know much!


## Hoon compiles itself

Unlike most programming languages, Hoon is actually written in itself.  Practically, this means that if there's anything we need to know or understand, we can just read the source code.


## An example

Let's walk through a trivial Hoon example, finding the minimum of two numbers. In `:dojo`:

```
> (min 4 2)
2
```

What if we want to know what actually happens under the hood here? The first thing we can do is look it up in the [`hoon.hoon`](https://github.com/urbit/arvo/blob/maint-20170614/arvo/hoon.hoon) source file.


#### Arms

An [*arm*](https://urbit.org/docs/hoon/concepts/#-core-object) `foo` within a larger [*core*](https://urbit.org/docs/hoon/concepts/#-core-object) is almost always signified by the syntax `++  foo` (with two spaces, or *gap*, between) in the relevant source file.

Search for `++  min` (with a gap) in `/arvo/hoon.hoon` with your editor, Unix `grep` or other preferred search tool to find the source arm:

```
++  min                                                 ::  minimum
  ~/  %min
  |=  {a/@ b/@}
  ^-  @
  ?:  (lth a b)  a
  b
```

Aside from the runes (ignore the `~/  %min` line), we have only three named symbols: `a`, `b` and `lth`.  `a` and `b` are our [sample](https://urbit.org/docs/hoon/concepts/#-gate-function), so `lth` must be another arm name.  Let's search for `++  lth`:

```
++  lth                                                 ::  less-than
  ~/  %lth
  |=  {a/@ b/@}
  ^-  ?
  ?&  !=(a b)
      |-
      ?|  =(0 a)
          ?&  !=(0 b)
              $(a (dec a), b (dec b))
  ==  ==  ==
```

> We want the `++lth` arm in the same *Basic Unsigned Math* section, chapter *2a*, as our `++min` arm. These are the ones used for atoms.

We see more runes here and also three named symbols: `a`, `b` and `dec`. `a` and `b` are again our sample. Let's look up `++dec`:

```
++  dec                                                 ::  decrement
  ~/  %dec
  |=  a/@
  ~|  %decrement-underflow
  ?<  =(0 a)
  =+  b=0
  |-  ^-  @
  ?:  =(a +(b))  b
  $(b +(b))
```

(Clearly, it's important to understand [the runes](https://urbit.org/docs/hoon/syntax/). We'll get to those in a second.)

Both `%dec` and `%decrement-underflow` are actually literals, so `++dec` is made entirely of Hoon primitives. There are no other symbols to track down.

We picked this example because it's pretty simple. In many cases you could find yourself a few levels deep in symbols.


## Molds

We can also search for [molds](https://urbit.org/docs/hoon/concepts/#-mold-type-as-constructor) in `hoon.hoon` to understand Hoon's type system a bit better. These are arms that structure unformatted data into typed data.

One mold we use a lot for string processing is the *tape*. A tape in Hoon is enclosed in double-quotes, like `"Hello, world!"`. Let's search for `++tape`:

```
++  tape  (list char)                                   ::  string as list
```

A valid tape is a list of characters. These of course are also defined in Hoon's source; let's look up `++list` and `++char`:

```
++  list  |*  a/mold                                    ::  null-term list
          $@($~ {i/a t/(list a)})                       ::

++  char  @tD                                           ::  UTF-8 byte
```

where the formal definition of a `++mold` in Hoon is:

```
++  mold  $-(* *)                                       ::  mold of a span
```

Like the Hoon standard library arms, often we'll find ourself levels deep in the Hoon type system as more complex molds are almost always built out of simpler molds.


## The runes: naturals and macros

Primitives in many popular programming languages are black boxes that do magic somehow. But the Hoon runes are just source too, and we can look at their code to see how they work.

Hoon gets compiled to Nock, which has only one primitive, ten instructions and one arithmetic operator. Hoon being [a very thin layer over Nock](https://urbit.org/docs/hoon/mission/), Hoon has only a few *natural runes*, and the rest are *macro runes* constructed from those.

One example is `|=`, or *bartis*, that acts as Hoon's simplest way to create a function which takes a *sample* and produces a *product*. All of the `hoon.hoon` functions in the last [Arms](#-arms) section use `|=`.

Search for `::  |=` (with a gap) in commented form to find the comment for bartis's mold in the `++twig` arm:

```
{$gate p/twig q/twig}                               :: |=
```

`|=` here is referred to by its [keyword](https://urbit.org/docs/hoon/syntax/#-keywords-versus-runes), `:gate`, with the symbol mold `$gate`.

`++ twig` is where the mold structures for all Hoon expressions are defined. In order to properly format a gate twig, we must pass it two twigs. You can find more on `|=` on its [rune docs page](https://urbit.org/docs/hoon/twig/bar-core/tis-gate).

So this is where `|=` is defined. What does it actually do? We can find this by searching for `$gate` and looking at the next place where it appears, in the `++open` arm:

```
{$gate *}  [%door p.gen (~(put by *(map term foot)) %$ [%ash q.gen])]
```


`++open` is the arm that gets called for all rune expansion. `++open` gets recursively called by the Hoon interpreter to expand macro runes until there are only "natural" runes left.

So the Hoon interpreter expands a gate to a *door*. We can figure out what a door is by looking up its mold by its symbol `$door` in `++twig`:

```
{$door p/twig q/(map term foot)}                    ::  |_
```

Upon which we can look up `++map`, `++term` and `++foot`:

```
++  map  |*  {a/mold b/mold}                            ::  associative tree
         $@($~ {n/{p/a q/b} l/(map a b) r/(map a b)})   ::

++  term  @tas                                          ::  ascii symbol

++  foot  $%  {$ash p/twig}                             ::  dry arm, geometric
              {$elm p/twig}                             ::  wet arm, generic
          ==                                            ::
```

If we cheat and look at the [door rune doc](https://urbit.org/docs/hoon/twig/bar-core/cab-door/), we can see that, like a gate, a door takes a mossy twig `p` as its sample. The difference is instead of one twig, a door takes a map `q` of many twigs tagged by either `%ash` or `%elm` for a dry or wet arm, respectively, and an additional ASCII symbol name for each of its arms.

So how does the Hoon interpreter expand a door? Let's look in `++open`:

```
{$door *}  [%pin [%bunt p.gen] [%core q.gen]]
```

So `|_` [*pins*](https://urbit.org/docs/hoon/twig/tis-flow/lus-pin) and [*bunts*](https://urbit.org/docs/hoon/twig/buc-mold) its sample `p` and constructs a core out of its map of terms to arms, `q`.

This helps tell us what `|=` does. A gate has a sample mold `p` and a single twig labeled `q`, which is its single dry arm of the empty name `$`. `|=` [*put*s](https://urbit.org/docs/hoon/library/2i/#-put-by) its single arm `q` in an empty map, referred to by term `$`, and constructs a door out of it using its same sample `p`. Then the Hoon interpreter expands this further to create a core.

`$core` is the natural rune here, defined in `++twig`:

```
{$core p/(map term foot)}                           ::  |%
```

It's a [fundamental Nock construct](https://urbit.org/docs/nock/explanation/#-9-invoke). Therefore, this makes `|=` a macro rune.

In short, a gate is a special case of a door, which is a special case of a core.

Almost all of the runes and irregular syntax in Hoon have expansions like this.


## Zuse: an example with JSON

The same process of course works for understanding [Zuse](https://github.com/urbit/arvo/blob/maint-20170614/arvo/zuse.hoon), the Arvo standard library. A good example is learning how to construct and parse JSON using the `++json` mold and various Zuse arms. This is something we do often when building Urbit web UI's.

Let's look up `++json` in `/arvo/zuse.hoon`:

```
++  json                                                ::  normal json value
          $@  $~                                        ::  null
          $%  {$a p/(list json)}                        ::  array
              {$b p/?}                                  ::  boolean
              {$o p/(map @t json)}                      ::  object
              {$n p/@ta}                                ::  number
              {$s p/@t}                                 ::  string
          ==                                            ::
```

This looks vaguely similar to the JSON of the JavaScript world. Let's construct a Hoon-typed JSON object in the `:dojo`:

```
> *@ta
~.

> ? ~.42
  @ta
~.42

> ? [%n ~.42]
  {$n @ta}
[%n ~.42]

> `json`[%n ~.42]
[%n p=~.42]

> *(map @t json)
{}

> (~(put by *(map @t json)) 'answer' `json`[%n ~.42])
{[p='answer' q=[%n p=~.42]]}

> `json`[%o (~(put by *(map @t json)) 'answer' `json`[%n ~.42])]
[%o p={[p='answer' q=[%n p=~.42]]}]
```

This seems a bit cumbersome. Zuse probably has an easier way of doing this.

A search for *"object"* in Zuse returns a few results, but the most promising are these two arms:

```
++  joba                                                ::  object from k-v pair
  |=  {p/@t q/json}
  ^-  json
  [%o [[p q] ~ ~]]

++  jobe                                                ::  object from k-v list
  |=  a/(list {p/@t q/json})
  ^-  json
  [%o (~(gas by *(map @t json)) a)]
```

Back in the `:dojo`:

```
> (joba 'answer' [%n ~.42])
[%o p={[p='answer' q=[%n p=~.42]]}]

> (jobe ~[['answer' [%n ~.42]] ['hello' [%s 'world']] ['foo' [%a ~[[%s 'bar'] [%s 'baz']]]]])
[ %o
    p
  { [p='answer' q=[%n p=~.42]]
    [p='hello' q=[%s p='world']]
    [p='foo' q=[%a p=~[[%s p='bar'] [%s p='baz']]]]
  }
]
```

To get something more familiar, we can use `pojo` (the JSON printer):

```
> (pojo (jobe ~[['answer' [%n ~.42]] ['hello' [%s 'world']] ['foo' [%a ~[[%s 'bar'] [%s 'baz']]]]]))
"{\"answer\":42,\"hello\":\"world\",\"foo\":[\"bar\",\"baz\"]}"
```

These are simple examples, but like arms in `hoon.hoon`, sometimes you can find yourself several levels deep trying to understand Zuse arms and the Arvo system. You get used to it.


## Cloning the docs

Lastly, sometimes it's hard to find arms we want by searching only the Hoon and Zuse source, even if those gates are actually in there. This can lead us down some weird rabbit holes, like `~taglux-nidsep` implementing his own uppercase function when it was there the whole time in `++cuss`.

One way to alleviate this problem is to clone the `docs` repo into our development ship's `web` directory. This not only allows us to self-host the docs and view them at `localhost` (which is by definition way faster than `urbit.org`), but it also exposes the `docs` repo to our editor/`grep` searches. This way, when we search for a keyword in our whole Arvo directory (which looks through Hoon and Zuse), we'll also return results for more detailed commentary and examples in the docs.

For example, we might want to understand how recursion works in Hoon. We get no luck searching for *"recursion"* or *"recurse"* in either `hoon.hoon` or Zuse (big words take up comment space!). But a search through our cloned docs repo returns plenty of results, including the doc for `|-`, *barhep* or `:loop`, which gives a simple recursive example.

## Use the source, Luke

You can use this learning pattern to understand all of Arvo's source, including the vanes and all userspace code like `:talk` and `:dojo`.

Overall, being able to look up all of Urbit's structures and functionality right in the source is pretty great. But it's not exactly obvious when to getting started. Hopefully this helps!

Feel free to ask questions in the comments, or come chat with us in `:talk`.

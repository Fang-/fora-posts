## `~narnet-naplyd-mirheb-lavsul--magfex-hapnut-rivteg-daplun`
The link above is broken now, so I'll paste the tutorial from there below.  ~taglux-nidsep
__________________

# how to hoon
 
# intro
Hoon is a statically typed, purely functional programming language. Imagine Haskell but without all that boring math and category theory stuff. While Haskell compiles to the lambda calculus, Hoon compiles to Nock: a simple combinator-based virtual machine based on binary trees of arbitrary precision integers.
 
The most important thing to remember when learning Hoon is /don't be afraid/. Hoon looks very different than other programming languages you've learned, probably. Instead of keywords and curly braces like C, Hoon uses runes. Runes are two-character diagraphs that map to the different statement types. When you read Hoon with no prior experience, you're going to try and scan over the code, ignoring all the weird symbols in favor of picking out variables, names, or other identifiers. /Try to avoid doing that./ Slowly go through every character. If you don't know what the specific rune does, try to estimate based on how all of them are grouped into families, and continue. One of the most prevalent comments I've heard from people who learn Hoon (including myself) is the amazement of just how /quickly/ they internalized the various runes.
 
In Hoon, all statements are expressions and reduce to a value. Declaring a variable, for example, isn't a full statement, because it doesn't return anything when you run the code, and in fact trying to run that would be a syntax error since Hoon expects another statement hanging off the end of it. Likewise, expressions are pure: they can /only/ reduce to a value, not modify any global state like C. If you want to modify an object in Hoon, you have to modify it and then return the mutated value.
 
First off, the base Hoon types. Hoon compiles to Nock, which isn't all that important to you as a programmer except for the reasoning behind various design decisions. But what it does give you are the three base types: Noun, Atom, and Cell. A Noun is an Atom or a Cell. An Atom is an unsigned arbitrary precision integer (aka they don't overflow, only grow in bit size). A Cell is an ordered pair of nouns (think cons cell if you know lisp, or just a two-size tuple if you don't). The definition of Cell is recursive, and so gives us the simple fact that Nouns are binary trees of bignums.
 
In Hoon terms, these types are `@` for atom, `^` for cell, and `*` for noun.
 
```
=/  a  1
=/  b/^  [2 3]
=/  c/*  4
[a b c]
```
 
The `=/` rune has three sub-expressions (call these twigs). The binding you are defining, including an optional type after a slash. The value to bind. And finally, the continuation of the program: in the case of the first line, this last expression is the entire rest of the program.
 
(Note that Hoon uses two spaces or a newline to seperate twigs! In fact, this is the majority of Hoon's syntax: there are no curly-braced blocks or significate indentation, only the seperation of different expressions into twigs, with runes taking `n` twigs under them. This prevents Hoon from needing Lisp's parenthesis hell.)
 
As said above, Hoon is strongly typed: if you attempted to bind `b` to just `2` you would get a type error, because `2` is not a cell. These three types are, essentially, all of Hoon's type system: more complex types are just build off these.
 
# The runes
Now, you might have looked at the description of runes and already be balking. Two random ASCII chacters instead of keywords? That's like expecting me to learn Chinese! In fact, all the runes fall into only a few categories, based off the first character.
 
## XX rune families
```
=  state modification
|  core creation
^  type casting
```
 
If you see `=-`, even though you haven't seen it before, you can still intuite that it will have something to do with changing the state. A majority of the runes are actually just macros that desugar into other, more basic runes, and are only there for convienence.
 
# Reading ++add
 
Great. Lots of background gotten out of the way. Let's actually jump into some Hoon code now. Here is the definition of the standard library function for adding two atoms.
 
```
|=  {a/@ b/@}
^-  @
?:  =(0 a)  b
$(a (dec a), b +(b))
```
 
First off, the first line. The `|` runes have to do with /cores/. Cores are the basis of Hoon's objects, modules, and functions: They are JavaScript-like objects where all the properties are computed when accessed. We refer to these properties as /arms/, and would refer to the arm `foo` as `++foo` (due to how they are written in Hoon). Functions are just cores that have an arm named `$` (or, in parlance, `++$`). Indeed, that's what the rune `|=` does: create a new function, taking the arguments and body that you define. We call functions with arguments /gates/, which are different from functions with no arguments. In this case, the arguments of the function are `{a/@ b/@}`: this is simply the type of a cell of two atoms, with the names `a` and `b` being the two elements in the cell. `{@ @}` is the type of the value `[1 1]`, for example.
 
`^-` is of the family for type casting. This simple says that the result of the function (gate!) should be of type atom. Because of the static typing, it's a very good idea to put a `^-` rune in all of your functions, in order to verify that your code is returning a value of the correct type - your code will work, but it won't be clear to people reading the code what type it returns, and if you make a mistake you may return an incorrect value without realizing it.
 
`?` is for conditionals. In this case, `?:` is simply the rune for if-then-else: you give it a condition to check, a true path, and a false path. In this case, if `a` is 0 then the function should return b, or else it should continue to the rest of the code.
 
If you're coming from a C-like language, you may be mistaken into thinking that `=(0 a)` is a function call. However, that's incorrect: Hoon uses Lisp-style function calls in the format of `(func a b ...)`. Instead, this line is the irregular form of a rune. If you look at the line, It's actually the irregular form of the rune `.=`, which tests equality of two nouns. There aren't all that many of these, and most of them you can guess what they do: `+(a)` increments a value, for example.
 
And now we hit the biggy. The last line is another irregular form of a rune (`%=` if you would like to know), and it's one of the most important things in Hoon: a(b c, d e) means "Evaluate the expression a, but with the value of b replaced with c, and d replaced with e". Because Hoon is purely functional, recursion is an important part of the language. First, remember that due to the `|=` rune this code is running inside an object with an arm named `$`. So `$(a 0)` would be "evaluate the arm we are in, with a set to the value 0". Broadly speaking, you will almost always see this type of irregular rune used to re-evaluate `$` in order to build recursion.
 
Now you can put it all together: this is a function that takes two numbers and returns a number. If `a` is zero it returns `b`, or else it returns the value of recursing, decrementing `a` and incrementing `b`.
 
# state
 
A few times I've refered to /state/. State is the entire world that your program is executing: the `=/`, which defines a new variable, is said to modify state because it sets state to a new state, which is the old one plus the new variable binding. State is analagous to all of RAM in a C program, and in Hoon is a noun itself! You can maybe see that this is the reason that Hoon is purely functional - if all modification of the state is simply changing a Hoon noun, and functions can only return a value, then when they return the modifications within scope are also dropped. In Hoon we call this global state your expressions are evaluated in the space of the /context/.
 
You can access the context from the `.` Hoon expression, which is actually just saying "give me the entire binary tree of the address space". There is in fact syntax to index arbitrary axises of the context, but it's generally considered bad form. You may see something like `<eny=0w000.000.000.000 our=~rilwyn-sanpyl <0.wa>>`, which is your shell's method of pretty-printing the state. What's important is that `=/  a  1  .` would show a=1 in your state, because `=/` is actually defining the context should be `[a=1 .]` - that is, cons the new variable on the head of it. Another thing to notice is that <0.wa> value, which is your shell's method of printing a core: because cores have properties that are computed, all properties are actually compiled Nock code and wouldn't be nice to print, and so it instead prints the number of arms in the core, followed by a short hash.
 
You may notice you have some big cores in your state. In fact, your state contains /the entire Hoon standard library/. Hoon is purely functional, and accessing the variable `a` in your code in fact pulls the address of `a` from state. Likewise, running `(add 1 2)` /also has to pull the function ++add from state/. Functions are values. This is very important.
 
Finally, defining a core captures the state by-value for all code inside. Let's try this.
 
```
=/  a  0
=/  test
  |.
  a
~&  (test)
.=  a  1
~&  (test)
a
```
 
(Quickly, `|.` defines a function with no arguments, `.=` modifies state, and `~&` is a debug printf that is good for prototyping)
We can see that when the function `test` is defined, it captured the entire state, including `a=0`. When we modify `a` in our code, we are only changing it at the current scope, and all captured environments keep their old value - when we re-run `test`, even though `a` changed outside, it's the same inside. This is the reason that functions in Hoon are pure: modifications inside of functions change the captured scope, which is dropped when the function returned, and in fact isn't even saved for future evaluations of the same function.
 
# inside a function
 
Now that we know how the context works, we can figure out how /functions/ work.
We know what functions are cores with an arm named `$`. We know that cores capture the environment at the point they are defined. Now we can learn the horribly lie: function arguments don't actually exist.
 
The desugared form of `|=  a/@  a`, for example, is actually:
 
```
=/  a/@  0
|%
  ++  $  a
--
```
 
(Core syntax is one of the only non-rune-like syntax in Hoon. Even though you use `++` and two space seperators, `++` isn't actually a rune. This is also why we refer to arm names as `++add`, for example.)
 
`|%` defines a core, followed by a list of arms and their bodies, terminated by `--`. What's weird about this is that we see that functions are a lie, and the only thing that adding an argument does is define a variable outside the core. This, in turn, means that the creation of the core captures a=0 inside it, and running `++$` would return `0`.
 
So we fall into another oddity of Hoon: all types have a default value. The default value of an atom, for example is just `0` - this is an interesting property (and lets us do some neat things later), but is important for us now because it fills in the slot in the binary tree of the core with a space that we can replace with the /actual/ value of `a` when we run the function. Actually calling the above core, for example, /sets the context's `a` inside the core and then evaluates `$` with the context set to the core itself/. The state /is the core/, a concrete Hoon value that can be touched and modified, not something that magically appears when calling specific functions. This solves problems like recursion or mutually recursive functions: the core is the noun that will become the state for all its arms, and so the arm will have a copy of itself in the state.
 
This also explains both why the context is captured (because the context is the core itself), why outside modifications aren't mirrored (beacuse you are only modifying the context, and not that value for all cores), and why modifying the value inside a function doesn't propagate that modification for future invocations of the function (because when you run the function it sets the context to the core's context and then returns the result, without setting the core's value to the new value when it returns).
 
Because `=/` always cons onto the head of the context when defining a variable, we always know what axis in the binary tree we have to modify in order to change that argument for the body of the function. This may feel hacky or ad hoc, but really isn't that different than function calls in C - it just accesses a specific address on the stack instead.
 
# objects
 
Objects are, in fact, /less/ specific types of functions. The general form for a object is as follows, and we're going to show how to use it.
 
```
=/  summer
|_  sum/@
  ++  abet  +<
  ::
  ++  this  .
  ::
  ++  inc
    this(sum +(sum)
  ::
  ++  plus
    |=  num/@
    this(sum (add sum num))
  ::
  ++  reset
    [sum  this(sum 0)]
--
```
(`::` are just comments  - it's common practice to use them between core arms, in order to visually seperate methods)
 
`|_` is the rune that defines an object core: it takes a type signature to keep as the "state" of the object - not the Hoon state, but "state" in the sense of a finite state machine, which we instead call the "sample", in this case, an atom named `sum`. In order to differentiate these cores-with-state from others, we call this a /door/.
 
Some idiosyncracies of Hoon: when you define an object, you should explicitly define `this`. As we said above, `.` is Hoon for "the entire state". Because arms are evaluated by setting the state to the core itself, `this` evaluates to /the entire core/ - returning `this` is the same as returning the core, and so `this(sum 0)` would be "return `this` with sum=0".
 
(We also define `++abet` - I don't actually know where this name comes from, but it's tradition to name it "abet". `+<` is that special syntax I mentioned above for indexing the state binary tree, and that specific slot is the location of the sample - `sum`)
 
How do we use this?
 
```
=/  obj  summer  ::  create the object (values in Hoon are immutable remember, so this is actually a new copy of it)
=.  obj  inc:obj  ::  set obj to the core returned from ++inc. a:b in Hoon means "the expression a evaluated in the context of b" - inc:obj is the arm `inc` in obj)
abet:obj  ::  return the sample from obj - which should be 1
```
 
Additionally, we have an example arm `++reset`. You often want to run a function on an object that both returns a value to the caller and modify state as well. `++reset` returns /two/ values: the sum so far, and the new object with a reset sum. We can't easily use just the normal `=.` for this, but luckily there is another rune made specifically for it.
 
```
=/  obj  summer
=.  obj  (sum:obj 4)
=^  count  obj  rest:obj
count
```
 
`=^` takes a name to bind a value to, and a value to replace. In this case, `count` is set to the first return value of `rest`, along with the rune updating `obj` to the new core like we did with `=.` This is a very important rune that most people learning Hoon don't understand, and so they don't properly learn how to leverage doors as objects.
 
Commonly, the default value of a type isn't actually what you want when you first create a core. If you wanted to start the sample at `100`, for example, you might have some trouble, especially for more complex doors. The irregular form of the rune `%~` helps with that:
 
```
~(. summer 100)
```
 
means "set the sample to 100, and then evaluate `.` in it". Since `.` is the context, and the context in a core is the core itself, this returns the core with a modified sample. You will also often see something like `(~(rad og 1.024) 0 255)` - this would create a new `++og` with the sample set, and return the `++rad` arm inside it for calling, for when you only need one arm in the core. Setting the sample this way is, naturally, type checked.
 
# how doors work
 
Now it's time for a little mindspin. When I said that doors are less specific types than functions, I meant it. Let's see what a door expands into.
 
```
|_  a/@
++  $  a
--
```
 
becomes
 
```
=/  a/@  0
|%
++  $  a
--
```
 
You might be feeling cheated right about now. In fact, we've already seen this "door" before - it's just the expanded form of the function `|=  a/@  a`! Indeed, /a function is a specific type of door/, one with an arm named `$`. That is the only difference. Running a function via `(func a b c)` is /an irregular rune itself/, and is /exactly equivalent/ to the code
 
```
=+  f=func
~($ f [a b c])
```
 
That is, create a new copy of the function `func`, set it's sample to `[a b c]`, and then evaluate the arm `$` in it. And just as function arguments work by capturing the state with a parameters defined right before, doors work by capturing the state with the /sample/ defined right before.
 
This is part of the reasoning behind why we call functions with arguments `gates` and argumentless functions `traps`, or the difference between a `door` and `core` - the method for calling them is different, because if you try to set the arguments in a trap, you would clobber the most recently defined variable in it instead.
 
And really, this is the most complex thing in Hoon. Intuiting that cores /really are/ Hoon's only complex data structure is the most difficult thing - everything else is just memorizing the runes and where to use them. All apps in Urbit, for example, are doors with arms that return a list of commands to execute, and a new app core.
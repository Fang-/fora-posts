## `~fittyn-fitsub-sigbex-hocdeb--misbud-hactyn-toddys-holryg`
`~sibnys`, I'm at a loss.  The only naughty thing about my program that I can see is it doesn't bother to cast anything.  (Admittedly I should have put casts in.)  But it should still run.  Try running the following version of the program instead:

```
!.                               :: Turn off stack trace
|=  n/@                          :: Start gate, let n be the input
=<  (goldbach-ce n)              :: Call "goldbach-ce" with n as sample
|%                               :: Form a core
++  prime                        :: Prime checking function
  |=  b/@                        :: For this gate, see "prime" post
  =/  i  2                       :: 
  =/  j  2                       :: 
  ^-  @f                         :: output for ++prime must be loobean
  ?:  (lth b 2)  %.n             :: 
  ?:  (lth b 4)  %.y             :: 
  |-  ^-  @f                     :: 
  ?:  =((mul i j) b)  %.n        :: 
  ?:  =((add j 1) b)  %.y        :: 
  ?:  (gth (mul i j) b)          :: 
    $(i 2, j (add j 1))          :: 
  $(i (add i 1))                 :: 
::                               :: 
++  bothprime                    :: Checks if a pair is prime
  |=  {b/@ c/@}                  :: b and c are the samples
  ^-  @f                         :: Output for ++bothprime is loobean
  ?:  &((prime b) (prime c))     :: If b and c are each prime then...
    %.y                          :: Return "yes"
  %.n                            :: otherwise "no"
::                               :: 
++  goldbach-ce                  :: The counterexample-testing gate
  |=  b/@                        :: b is the sample
  ^-  *                          :: Output is some noun
  ?:  (lth b 4)                  :: If b < 4 then...
    %.n                          :: ...return "no"
  ?:  =((mod b 2) 1)             :: If b isn't even then...
    %.n                          :: ...return "no"
  =/  i  2                       :: let i = 2
  =/  j  (dec (dec b))           :: let j = b - 2. (Note: i + j = b)
  |-  ^-  *                      :: Start of loop
  ?:  (bothprime [i j])          :: If i and j are both prime then...
    [[i j] %.n]                  :: ...return i and j, and "no"
  ?:  =((add i 2) b)             :: If i + 2 = b then...
    %.y                          :: ...return "yes", otherwise...
  $(i (add 1 i), j (dec j))      :: Add 1 to i, take 1 from j, and loop
--                               :: End of battery (for core)
```

Does anyone else run into the same problem?
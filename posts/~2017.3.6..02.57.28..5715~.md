---
type: post
date: ~2017.3.6..02.57.28..5715
title: even number checker in nock
author: ~taglux-nidsep
navsort: bump
navuptwo: true
comments: reverse
---

This time we'll make a nock program that takes as input some unsigned integer N, and returns 0 if it's even, otherwise 1.

Pseudo-code algorithm:

    i = 0
    j = 1
    
    :loop start
    If i = N then return 0
    If j = N then return 1
    Add 2 to i, add 2 to j, and goto :loop start

The idea is simple.  i starts as 0, the smallest even, and j starts as 1, the smallest odd.  We check if N is one of these.  If so, we know whether N is even or not.  Otherwise, add two to each of i and j and try again.  Eventually one of them will equal N, and then we'll know whether the latter is even.

Let's get to it!

`[8 GB1 w]`

GB1 is going to add our library to the subject.  Because this is a simple function, there is only going to be one thing in the library: the even number checker itself.  (We'll be using this in a later program.)

GB1 is of the form: `[[1 GB1* ] [1 0]`

...so the subject being passed to w is:  `[[GB1* 0] N]`

The tuple on the left is the library.  Next, w must produce the even number checker (ENC) gate and then call it.

w:

`[8 [9 2 [0 2]] y]`

This takes the library, `[GB1* 0]`, and passes it to GB1*, which is of the following form:

`[[1 [ENC arm]] [[[1 0] [1 1]] [1 0]]]`

The tuple `[[1 0] [1 1]]` is for our variables i and j, which have the initial values of 0 and 1, respectively.

The result of `[9 2 [0 2]]` is:

`[[ENC arm] [[0 1] z]]`, where z is the library, `[[[1 [ENC arm]] [[[1 0] [1 1]] [1 0]]] 0]`.

So the subject being passed to y is: `[[[ENC arm] [[0 1] z]] [z N]]`

y, 'call the ENC arm':

`[9 2 [[0 4] [[0 7] [0 5]]]]`

By convention, when we call an arm we must pass it a subject of the form:

[[arm] [[argument(s)] [context]]]

Which we do:

arm: `[0 4]` => `[ENC arm]`

argument: `[0 7]` => `N`

context: `[0 5]` => `[[0 1] z]]`   ::   The tuple [0 1] is for the initial values of i and j, respectively.

This whole subject, `[ENC arm] [N [[0 1] z]]]`, is passed to `[ENC arm]`.

Let's go back and figure out what GB1 is.  (Remember, GB1 is the ancestor of GB1* , from which we want to get `[ENC arm]`.)

GB1:

`[[1 [[1 [6 a b c]] [[[1 0] [1 1]] [1 0]]]] [1 0]]`

The only new part is `[6 a b c]`, which is `[ENC arm]`.  We want it to match the first conditional statement of the pseudo-code algorithm above: "If i = N then return 0 (otherwise c)"

a, 'i = N':

`[5 [0 28] [0 6]]`

This is straightforward.  Address # 28 of the subject is for i, and address # 6 is N.

b, 'return 0':

`[1 0]`

Easy!

c, 'If j = N then return 1, else d':

`[6 [5 [0 29] [0 6]] [1 1] d]`

Also straightforward.  Address # 29 is for j, and # 6 is still N.

d, 'add 2 to i, add 2 to j, and loop':

`[9 2 [[0 2] [[0 6] [e [0 15]]]]]`

We loop by calling `[ENC arm]` and passing it the same subject, except that we want to increment i and j twice each.  In d we pass everything in the subject unchanged, leaving the increment work for e.

e, '[i+2 j+2]':

`[[4 [4 [0 28]]] [4 [4 [0 29]]]]`

Not too difficult.  We simply stack two increment functions for each of i and j.  And that's it!

All of GB1:

`[1 [[1 [6 [5 [0 28] [0 6]] [1 0] [6 [5 [0 29] [0 6]] [1 1] [9 2 [[0 2] [[0 6] [[[4 [4 [0 28]]] [4 [4 [0 29]]]] [0 15]]]]]]]] [[[1 0] [1 1]] [1 0]]]]`

The whole program:

`[8 [[1 [[1 [6 [5 [0 28] [0 6]] [1 0] [6 [5 [0 29] [0 6]] [1 1] [9 2 [[0 2] [[0 6] [[[4 [4 [0 28]]] [4 [4 [0 29]]]] [0 15]]]]]]]] [[[1 0] [1 1]] [1 0]]]] [1 0]] [8 [9 2 [0 2]] [9 2 [[0 4] [[0 7] [0 5]]]]]]`

In a more readable format:

    [ 8
      [ [ 1
          [ 1
            6
                [5 [0 28] 0 6]
                [1 0]
                6
                    [5 [0 29] 0 6]
                    [1 1]
                    [9 2 [[0 2] [0 6] [[4 4 0 28] [4 4 0 29]] [0 15]]]
          ]
          [[1 0] [1 1]]
          [1 0]
        ]
        [1 0]
      ]
      8
          [9 2 [0 2]]
          [9 2 [[0 4] [0 7] [0 5]]]
    ]

To test whether 45 is even in dojo, enter:

`.*(45 [8 [[1 [[1 [6 [5 [0 28] [0 6]] [1 0] [6 [5 [0 29] [0 6]] [1 1] [9 2 [[0 2] [[0 6] [[[4 [4 [0 28]]] [4 [4 [0 29]]]] [0 15]]]]]]]] [[[1 0] [1 1]] [1 0]]]] [1 0]] [8 [9 2 [0 2]] [9 2 [[0 4] [[0 7] [0 5]]]]]])`

It should return 1 for "no".

We'll be using this function for other purposes soon!
 